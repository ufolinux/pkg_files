pkgname=gtk4+
pkgver=4.6.4
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit"
url="https://www.gtk.org/"
arch=(x86_64)
depends=(glib2 cairo pango harfbuzz fribidi gdk-pixbuf libpng libtiff libjpeg
         libepoxy libglvnd egl-wayland libxkbcommon graphene iso-codes
         colord wayland libxrandr libx11 libxrender libxi libxext libxcursor
         libxdamage libxfixes fontconfig libxinerama cups
         gstreamer librsvg dconf shared-mime-info desktop-file-utils
         adwaita-icon-theme cantarell-fonts gst-plugins-bad)
makedepends=(gobject-introspection gtk-doc git glib2 sassc meson
	wayland-protocols gi-docgen)
commitid=1e6bad6c4f412dc0eb7b2f508cb4465929a04303  # tags/4.6.4^0
source=("git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$commitid"
        gtk-update-icon-cache.{hook,script} gtk4-querymodules.hook)

build() {
  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  local meson_options=(
    --prefix=/usr
    --wrap-mode=nofallback
    -D broadway-backend=true
    -D cloudproviders=disabled
    -D tracker=disabled
    -D colord=enabled
    -D gtk_doc=false
    -D man-pages=false
  )

  meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-4.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk4-querymodules.hook
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-update-icon-cache.hook
  install -D gtk-update-icon-cache.script "$pkgdir/usr/share/libalpm/scripts/gtk-update-icon-cache"
}
