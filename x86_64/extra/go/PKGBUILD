pkgname=go
pkgver=1.18.3
pkgrel=1
arch=('x86_64')
makedepends=(git perl go)
source=(https://go.dev/dl/go$pkgver.src.tar.gz)

build() {
	export GOARCH=amd64
	export GOAMD64=v1 # make sure we're building for the right x86-64 version
	export GOROOT_FINAL=/usr/lib/go
	export GOROOT_BOOTSTRAP=/usr/lib/go

	cd "$pkgname/src"
	./make.bash -v
}

package() {
	cd $pkgname

	install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib/go" "$pkgdir/usr/share/doc/go" \
		"$pkgdir/usr/lib/go/pkg/linux_amd64_"{dynlink,race}

	cp -a bin pkg src lib misc api test "$pkgdir/usr/lib/go"
	cp -r doc/* "$pkgdir/usr/share/doc/go"

	ln -sf /usr/lib/go/bin/go "$pkgdir/usr/bin/go"
	ln -sf /usr/lib/go/bin/gofmt "$pkgdir/usr/bin/gofmt"
	ln -sf /usr/share/doc/go "$pkgdir/usr/lib/go/doc"

	install -Dm644 VERSION "$pkgdir/usr/lib/go/VERSION"

	rm -rf "$pkgdir/usr/lib/go/pkg/bootstrap" "$pkgdir/usr/lib/go/pkg/tool/*/api"

	# TODO: Figure out if really needed
	rm -rf "$pkgdir"/usr/lib/go/pkg/obj/go-build/*
}
