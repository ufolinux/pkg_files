pkgname=openal
pkgver=1.21.1
pkgrel=1
arch=(x86_64)
depends=(alsa-lib pulseaudio jack2 qt5 sdl2
	libsndfile ffmpeg)
makedepends=(git cmake ninja)
commitid=ae4eacf147e2c2340cc4e02a790df04c793ed0a9  # 1.21.1
source=("git+https://github.com/kcat/openal-soft#commit=$commitid"
	ffmpeg-5.0.diff)

prepare() {
	cd openal-soft

	# Missing include
	git cherry-pick -n 302e88dbf0f10224a5b87be4ce43b3fdd9d20184

	# https://bugs.archlinux.org/task/72729
	git cherry-pick -n b7ff1de48efda9ad54e6a3b4c1526722c371e832

	# Fix build with FFmpeg 5.0
	git apply -3 ../ffmpeg-5.0.diff
}

build() {
	cmake -S openal-soft -B build -G Ninja \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D CMAKE_BUILD_TYPE=None \
	-D CMAKE_INSTALL_LIBDIR=lib

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
