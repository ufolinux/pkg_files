pkgname=cups-filters
pkgver=1.28.15
pkgrel=1
arch=('x86_64')
depends=('lcms' 'poppler' 'cups' 'systemd' 'libpng'
	'libtiff' 'libjpeg' 'qpdf' 'avahi' 'ghostscript' 'mupdf')
makedepends=('python3')
backup=(etc/cups/cups-browsed.conf)
source=(https://www.openprinting.org/download/cups-filters/$pkgname-$pkgver.tar.xz)

build() {
    cd "$pkgname"-$pkgver

    ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --with-rcdir=no \
    --with-browseremoteprotocols=DNSSD,CUPS \
    --enable-auto-setup-driverless

    make
}

package() {
    cd "$pkgname"-$pkgver
    make DESTDIR="$pkgdir/" install
  
    # add upstream systemd support file
    install -Dm644 utils/cups-browsed.service "${pkgdir}"/usr/lib/systemd/system/cups-browsed.service
    sed -i "s|/usr/sbin/cups-browsed|/usr/bin/cups-browsed|" "${pkgdir}"/usr/lib/systemd/system/cups-browsed.service
  
    # use cups group from cups pkg FS#56818
    chgrp -R 209 "${pkgdir}"/etc/cups
}
