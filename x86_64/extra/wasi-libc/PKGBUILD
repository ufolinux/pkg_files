pkgname=wasi-libc
pkgver=0+222+ad51334
pkgrel=2
_commit=ad5133410f66b93a2381db5b542aad5e0964db96  # Adjust the Makefile for LLVM 13 
pkgdesc='WASI libc implementation for WebAssembly'
arch=('x86_64')
url='https://github.com/WebAssembly/wasi-libc'
makedepends=('git' 'llvm' 'clang')
source=("git+https://github.com/WebAssembly/wasi-libc.git#commit=${_commit}")
options=('staticlibs')

pkgver() {
	cd ${pkgname}

	printf "0+%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd ${pkgname}

	make \
	WASM_CC=/usr/bin/clang \
	WASM_AR=/usr/bin/llvm-ar \
	WASM_NM=/usr/bin/llvm-nm
}

package() {
  cd ${pkgname}

  install -dm755 "${pkgdir}"/usr/share

  cp -dr --preserve=mode,timestamp sysroot "${pkgdir}"/usr/share/wasi-sysroot
}
