pkgname=libva
pkgver=2.13.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=(x86_64)
url=https://01.org/linuxmedia/vaapi
license=(MIT)
depends=(
	libdrm
	libx11
	libxext
	libxfixes
	wayland
)
makedepends=(
	git
	libglvnd
	mesa
	meson
)

backup=(etc/libva.conf)
_tag=8d948cc076cb255b442afe4f0f4e04cbc80785bf
source=(git+https://github.com/intel/libva.git#tag=${_tag})

build() {
	CFLAGS+=" -DENABLE_VA_MESSAGING"  # Option missing
	meson libva build	\
	--prefix=/usr		\
	--buildtype=release

	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" meson install -C build

	install -Dm 644 /dev/stdin "${pkgdir}"/etc/libva.conf <<END
LIBVA_MESSAGING_LEVEL=1
END
}
