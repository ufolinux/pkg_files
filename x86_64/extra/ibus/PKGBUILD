pkgname=ibus
pkgver=1.5.25
pkgrel=3
arch=('x86_64')
license=('LGPL')
depends=('dconf' 'gtk3+' 'hicolor-icon-theme' 'libnotify' 'iso-codes' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'gtk-doc' 'gtk2+' 'gnome-common'
    'qt5' 'git' 'unicode-character-database' 'unicode-cldr' 'unicode-emoji' 'iso-codes' 'python3-pygments')
options=('!emptydirs')
_commit=cfb263f04f82a83dd94f6eedcd5017d40a4a1a05  # 1.5.25
source=("git+https://github.com/ibus/ibus#commit=$_commit")

prepare() {
    cd ibus

    sed -i 's|$(libibus) $(libibus_emoji_dialog)|$(libibus_emoji_dialog) $(libibus)|' ui/gtk3/Makefile.am
    git cherry-pick -n 936a0e76df79d92a8bdc03e9205330fb84a2083e

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd ibus

    ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --enable-dconf \
    --enable-wayland \
    --enable-gtk-doc \
    --disable-memconf \
    --enable-ui \
    --disable-python2 \
    --with-python=python3

    make
}

package_ibus() {
    cd ibus

    make DESTDIR="$pkgdir" install
    make -C src DESTDIR="$pkgdir" install
    make -C bindings DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" install-pkgconfigDATA
}
