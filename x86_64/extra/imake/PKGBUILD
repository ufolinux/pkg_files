pkgname=imake
pkgver=1.0.8
pkgrel=1
pkgdesc="X.Org imake program and related utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('pkgconf' 'xorgproto' 'perl' 'util-macros' 'font-util')
source=(https://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.bz2
	https://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.3.tar.bz2
	https://xorg.freedesktop.org/releases/individual/util/lndir-1.0.3.tar.bz2
	https://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.6.tar.bz2
	https://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.6.tar.bz2
	linuxconfig.patch)

prepare() {
	cd xorg-cf-files-1.0.6
	patch -p1 -i ../linuxconfig.patch
}

build() {
	for i in *; do
		if [ -d "${i}" ]; then
			pushd "${i}"
			autoreconf -fi
			./configure --prefix=/usr --mandir=/usr/share/man
			make
			popd
		fi
	done
}

package() {
	for i in *; do
		if [ -d "${i}" ]; then
			pushd "${i}"
			make DESTDIR="${pkgdir}" install
			popd
		fi
	done
}
