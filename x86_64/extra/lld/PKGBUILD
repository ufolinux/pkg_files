pkgname=lld
pkgver=13.0.0
pkgrel=2
pkgdesc="Linker from the LLVM project"
arch=('x86_64')
url="https://lld.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm')
makedepends=('llvm' 'cmake' 'ninja')
options=('!lto') # LTO causes a couple of test failures
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/$pkgname-$pkgver.src.tar.xz
	$_source_base/llvm-$pkgver.src.tar.xz
	$_source_base/libunwind-$pkgver.src.tar.xz)

prepare() {
	# https://bugs.llvm.org/show_bug.cgi?id=49228
	mv libunwind{-$pkgver.src,}

	cd "$srcdir/$pkgname-$pkgver.src"
	mkdir -p build
}

build() {
	cd "$srcdir/$pkgname-$pkgver.src/build"

	cmake .. -G Ninja \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DLLVM_LINK_LLVM_DYLIB=ON \
	-DLLVM_INCLUDE_TESTS=ON \
	-DLLVM_BUILD_TESTS=OFF \
	-DLLVM_BUILD_DOCS=OFF \
	-DLLVM_ENABLE_SPHINX=OFF \
	-DSPHINX_WARNINGS_AS_ERRORS=OFF \
	-DLLVM_EXTERNAL_LIT=/usr/bin/lit \
	-DLLVM_MAIN_SRC_DIR="$srcdir/llvm-$pkgver.src"

	ninja
}

package() {
	cd "$srcdir/$pkgname-$pkgver.src/build"

	DESTDIR="$pkgdir" ninja install
	install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# https://bugs.llvm.org/show_bug.cgi?id=42455
	install -Dm644 -t "$pkgdir/usr/share/man/man1" ../docs/ld.lld.1
}
