pkgname=unicode-character-database
pkgver=14.0.0
pkgrel=1
arch=(x86_64)
makedepends=(lynx)
source=("UCD-$pkgver.zip::https://www.unicode.org/Public/zipped/$pkgver/UCD.zip"
        "Unihan-$pkgver.zip::https://www.unicode.org/Public/zipped/$pkgver/Unihan.zip"
        "unicode-license.html::https://www.unicode.org/license.html")
noextract=({UCD,Unihan}-$pkgver.zip)

build() {
    lynx -dump -display_charset=utf-8 unicode-license.html > LICENSE
}

package() {
    for _f in UCD Unihan; do
        install -Dm644 $_f-$pkgver.zip "$pkgdir/usr/share/unicode/$_f.zip"
        bsdtar -C "$pkgdir/usr/share/unicode" -x --no-same-owner --no-same-permissions -f $_f-$pkgver.zip
    done

    ln -s . "$pkgdir/usr/share/unicode/ucd"
    for _f in $pkgname unicode-data unidata; do
        ln -s unicode "$pkgdir/usr/share/$_f"
    done
}
