pkgname=sqlite3
pkgver=3.37.0
sver=3370000
pkgrel=1
arch=('x86_64')
makedepends=(readline zlib tcl)
source=(https://sqlite.org/2021/sqlite-src-$sver.zip)

build() {
	export CPPFLAGS="$CPPFLAGS		\
	-DSQLITE_ENABLE_COLUMN_METADATA=1	\
	-DSQLITE_ENABLE_UNLOCK_NOTIFY=1		\
	-DSQLITE_ENABLE_DBSTAT_VTAB=1		\
	-DSQLITE_ENABLE_FTS3_TOKENIZER=1	\
	-DSQLITE_SECURE_DELETE			\
	-DSQLITE_ENABLE_STMTVTAB		\
	-DSQLITE_MAX_VARIABLE_NUMBER=250000	\
	-DSQLITE_MAX_EXPR_DEPTH=10000		\
	-DSQLITE_ENABLE_MATH_FUNCTIONS"

	cd sqlite-src-$sver
	./configure		\
	--prefix=/usr		\
	--disable-amalgamation	\
	--enable-fts3		\
	--enable-fts4		\
	--enable-fts5		\
	--enable-rtree		\
	--enable-json1		\
	TCLLIBDIR=/usr/lib/sqlite$pkgver

	make
	make showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer
}

package() {
	cd sqlite-src-$sver

	make DESTDIR=$pkgdir install

	install -m755 showdb showjournal showstat4 showwal sqldiff $pkgdir/usr/bin/
}
