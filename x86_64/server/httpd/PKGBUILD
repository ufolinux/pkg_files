pkgname=httpd
pkgver=2.4.53
pkgrel=1
arch=(x86_64)
depends=(zlib apr-util pcre nghttp2 openssl lua libxml2 curl jansson brotli lynx perl)
makedepends=(libxml2 lua curl brotli jansson)
options=(emptydirs)
source=(https://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2
	https://github.com/archlinux/svntogit-packages/raw/packages/apache/trunk/openssl-malloc-init.patch
	apache.tmpfiles.conf
	httpd.service
	httpd.sysusers	
	ufo.layout)

prepare() {
	cd $pkgname-$pkgver

	# set default user
	sed -e 's#User daemon#User http#' \
	-e 's#Group daemon#Group http#' \
	-i docs/conf/httpd.conf.in

	cat "${srcdir}/ufo.layout" >> config.layout

	# https://github.com/openssl/openssl/issues/2865
	patch -Np1 -i ../openssl-malloc-init.patch
}

build() {
	cd $pkgname-$pkgver

	./configure \
	--enable-layout=Ufo \
	--enable-mpms-shared=all \
	--enable-modules=all \
	--enable-mods-shared=all \
	--enable-so \
	--enable-suexec \
	--with-suexec-caller=http \
	--with-suexec-docroot=/srv/http \
	--with-suexec-logfile=/var/log/httpd/suexec.log \
	--with-suexec-uidmin=99 --with-suexec-gidmin=99 \
	--enable-ldap --enable-authnz-ldap --enable-authnz-fcgi \
	--enable-cache --enable-disk-cache --enable-mem-cache --enable-file-cache \
	--enable-ssl --with-ssl \
	--enable-deflate --enable-cgi --enable-cgid \
	--enable-proxy --enable-proxy-connect \
	--enable-proxy-http --enable-proxy-ftp \
	--enable-dbd --enable-imagemap --enable-ident --enable-cern-meta \
	--enable-lua --enable-xml2enc --enable-http2 \
	--enable-proxy-http2 --enable-md --enable-brotli \
	--with-apr=/usr/bin/apr-1-config \
	--with-apr-util=/usr/bin/apu-1-config \
	--with-pcre=/usr

  make
}

package() {
	cd $pkgname-$pkgver

	make DESTDIR=$pkgdir install

	install -D -m644 "${srcdir}/apache.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/apache.conf"
	install -D -m644 "${srcdir}/httpd.service" "${pkgdir}/usr/lib/systemd/system/httpd.service"

	# Systemd http sysuser
	install -D -m644 "$srcdir"/httpd.sysusers $pkgdir/usr/lib/sysusers.d/httpd.conf

	# Make prefix dir
	mkdir -p $pkgdir/opt/httpd
}
