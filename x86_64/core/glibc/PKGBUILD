pkgname=glibc
pkgver=2.35
pkgrel=4
arch=('x86_64')
depends=(libxcrypt filesystem lib32-libxcrypt)
makedepends=(python3 bison m4)
options=(!strip emptydirs staticlibs)
backup=(etc/gai.conf
	etc/locale.gen
	etc/nscd.conf)
install=glibc.install
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz)

build() {
	cd $pkgname-$pkgver

	mkdir -pv build
	cd build

	../configure \
	--prefix=/usr \
	--disable-werror \
	--enable-add-ons \
	--enable-bind-now \
	--enable-cet \
	--enable-lock-elision \
	--enable-stack-protector=strong \
	--enable-stackguard-randomization \
	--enable-kernel=3.2 \
	--enable-stack-protector=strong \
	--with-headers=/usr/include \
	--disable-crypt \
	--enable-multi-arch \
	libc_cv_slibdir=/lib

	make
	
	mkdir -p ../build32
	cd ../build32
	CC="gcc -m32" \
	CXX="g++ -m32" \
	../configure \
	--prefix=/usr \
	--disable-werror \
	--enable-kernel=3.2 \
	--enable-multi-arch \
	--enable-stack-protector=strong \
	--libdir=/usr/lib32 \
	--disable-crypt \
	--enable-add-ons \
	--libexecdir=/usr/lib32 \
	libc_cv_slibdir=/usr/lib32 \
	i686-pc-linux-gnu

	make

}

package() {
        # 64bit install
        cd $pkgname-$pkgver
        cd build

        mkdir -p $pkgdir/lib64
        ln -sfv ../lib/ld-linux-x86-64.so.2 $pkgdir/lib64/ld-linux-x86-64.so.2
        ln -sfv ../lib/ld-linux-x86-64.so.2 $pkgdir/lib64/ld-lsb-x86-64.so.3

        mkdir -p $pkgdir/etc
        touch $pkgdir/etc/ld.so.conf
        make install_root=$pkgdir install

        cp -v ../nscd/nscd.conf $pkgdir/etc/nscd.conf
        mkdir -pv $pkgdir/var/cache/nscd

        mkdir -pv $pkgdir/usr/lib/locale
        make install_root=$pkgdir localedata/install-locales -j2

	# 32bit
	cd ../build32	
	
	rm -rf DESTDIR	
	mkdir -p DESTDIR/usr/lib32/
	sleep 10

	make install_root=$('pwd')/DESTDIR install
	
	install -vdm755 $pkgdir/usr/lib32
	install -vdm755 $pkgdir/include/gnu
	install -vdm755 $pkgdir/etc/ld.so.conf.d

	find DESTDIR/usr/include -type f -not -name '*-32.h' -delete
	cp -rf DESTDIR/usr/include/* $pkgdir/usr/include/

	mkdir -vp $pkgdir/usr/lib32/
	cp -Rv DESTDIR/usr/lib32/* $pkgdir/usr/lib32/

	# Provided by libxcrypt; keep the old shared library for backwards compatibility
        rm -f "$pkgdir"/usr/include/crypt.h "$pkgdir"/usr/{lib,lib32}/libcrypt.{a,so}

	mkdir -vp $pkgdir/etc/ld.so.conf.d
	mkdir -vp $pkgdir/lib/

	ln -sv ../usr/lib32/ld-linux.so.2 $pkgdir/lib/ld-linux.so.2
	ln -sv ../usr/lib32/ld-linux.so.2 $pkgdir/lib/ld-lsb.so.3
	ln -sv ../lib/locale $pkgdir/usr/lib32/locale

	mkdir -p $pkgdir/etc/ld.so.conf.d
	echo "/usr/lib32" > "$pkgdir/etc/ld.so.conf.d/lib32-glibc.conf"

	cat > $pkgdir/etc/nsswitch.conf << "EOF"
# Begin /etc/nsswitch.conf

passwd: files
group: files
shadow: files

hosts: files dns
networks: files

protocols: files
services: files
ethers: files
rpc: files

# End /etc/nsswitch.conf
EOF

cat > $pkgdir/etc/ld.so.conf << "EOF"
# Begin /etc/ld.so.conf
/usr/local/lib
/opt/lib

EOF

cat >> $pkgdir/etc/ld.so.conf << "EOF"
# Add an include directory
include /etc/ld.so.conf.d/*.conf

EOF
	mkdir -pv $pkgdir/etc/ld.so.conf.d

	# Stripping
	if check_option 'debug' n; then
		find "$pkgdir"/usr/bin -type f -executable -exec strip $STRIP_BINARIES {} + 2> /dev/null || true
		find "$pkgdir"/usr/lib -name '*.a' -type f -exec strip $STRIP_STATIC {} + 2> /dev/null || true

		# Do not strip these for gdb and valgrind functionality, but strip the rest
		find "$pkgdir"/usr/lib \
			-not -name 'ld-*.so' \
			-not -name 'libc-*.so' \
			-not -name 'libpthread-*.so' \
			-not -name 'libthread_db-*.so' \
			-name '*-*.so' -type f -exec strip $STRIP_SHARED {} + 2> /dev/null || true
		fi
}
