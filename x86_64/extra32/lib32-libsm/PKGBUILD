_pkgbasename=libsm
pkgname=lib32-$_pkgbasename
pkgver=1.2.3
pkgrel=1
pkgdesc="X11 Session Management library (32-bit)"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('lib32-libice' 'lib32-util-linux' $_pkgbasename)
makedepends=('util-macros' 'libxtrans')
source=(${url}/releases/individual/lib/libSM-${pkgver}.tar.bz2)

build() {
	cd "${srcdir}/libSM-${pkgver}"

	export CC="gcc -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32

	make
}

package() {
	cd "${srcdir}/libSM-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}"/usr/{include,share}
}
