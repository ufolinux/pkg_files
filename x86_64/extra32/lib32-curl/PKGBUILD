pkgname=lib32-curl
pkgver=7.81.0
pkgrel=1
pkgdesc='An URL retrieval utility and library (32-bit)'
arch=('x86_64')
url='https://curl.haxx.se'
depends=('curl' 'lib32-brotli' 'lib32-libidn2' 'lib32-zlib' 'lib32-zstd' 'lib32-openssl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz")

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	mkdir -p build && cd build

	../curl-${pkgver}/configure \
	--prefix='/usr' \
	--enable-threaded-resolver \
	--with-gssapi \
	--with-openssl \
	--with-random='/dev/urandom' \
	--with-ca-bundle='/etc/ssl/certs/ca-certificates.crt' \
	--libdir='/usr/lib32'

	make
}

package() {
	cd build

	make DESTDIR="${pkgdir}" install

	rm -rf $pkgdir/usr/{bin,include,share}
}
