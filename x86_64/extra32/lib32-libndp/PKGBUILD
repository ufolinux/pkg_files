pkgname=lib32-libndp
pkgver=1.8
pkgrel=1
pkgdesc='Library for Neighbor Discovery Protocol'
arch=('x86_64')
depends=('libndp')
makedepends=('git')
source=("git+https://github.com/jpirko/libndp#tag=v${pkgver}")

prepare() {
	cd libndp

	./autogen.sh
}

build() {
	cd libndp

	export CC='gcc -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	./configure \
	--prefix='/usr' \
	--libdir='/usr/lib32' \
	--libexecdir='/usr/lib32' \
	--localstatedir='/var' \
	--sysconfdir='/etc' 

	make
}

package() {
	make DESTDIR="${pkgdir}" -C libndp install

	rm -rf "${pkgdir}"/usr/{bin,include,share}
}