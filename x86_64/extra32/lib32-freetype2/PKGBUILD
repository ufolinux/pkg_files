pkgname=lib32-freetype2
pkgver=2.11.1
pkgrel=1
pkgdesc="Font rasterization library (32-bit)"
url="https://www.freetype.org/"
arch=(x86_64)
depends=(lib32-zlib lib32-bzip2 lib32-libpng lib32-brotli freetype2)
makedepends=(meson)
source=(https://download-mirror.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz{,.sig}
	0001-Enable-table-validation-modules.patch
	0002-Enable-subpixel-rendering.patch
	0003-Enable-infinality-subpixel-hinting.patch
	0004-Enable-long-PCF-family-names.patch)

prepare() {
	cd freetype-$pkgver
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
	patch -Np1 -i ../0004-Enable-long-PCF-family-names.patch
}

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

	meson freetype-$pkgver build \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	-D default_library=shared

	meson compile -C build
}

package() {
	meson install -C build --destdir="$pkgdir"

	rm -r "$pkgdir"/usr/include
}
