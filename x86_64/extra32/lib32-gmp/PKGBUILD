_pkgbasename=gmp
pkgname=lib32-$_pkgbasename
pkgver=6.2.1
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic (32-bit)"
arch=('x86_64')
url="https://gmplib.org/"
depends=($_pkgbasename)
source=(https://gmplib.org/download/gmp/gmp-${pkgver}.tar.xz)

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd "${_pkgbasename}-${pkgver}"

	export ABI=32

	./configure \
	--prefix=/usr --infodir=/usr/share/info \
	--enable-fat --enable-cxx --libdir=/usr/lib32 \
	--includedir=/usr/lib32/gmp

	#Put gmp.h in the same folder as gmpxx.h
	sed -i 's/$(exec_prefix)\/include/$\(includedir\)/' Makefile

	make
}

package() {
	cd "${_pkgbasename}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}"/usr/{include,share,bin}
}