pkgname=lib32-sqlite3
pkgver=3.37.2
srcver=3370200
pkgrel=1
arch=('x86_64')
depends=(sqlite3)
makedepends=(lib32-readline)
source=(https://www.sqlite.org/2022/sqlite-src-${srcver}.zip
	sqlite3-amalgamation_configure.patch)

prepare() {
	cd sqlite-src-$srcver

	patch -Np1 -i ../sqlite3-amalgamation_configure.patch

	autoreconf -vfi
}

build() {
	cd sqlite-src-$srcver

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	export LTLINK_EXTRAS="-ldl"
	export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 \
                         -DSQLITE_ENABLE_COLUMN_METADATA=1 \
                         -DSQLITE_ENABLE_UNLOCK_NOTIFY \
                         -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
                         -DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
                         -DSQLITE_ENABLE_RTREE=1 \
                         -DSQLITE_SECURE_DELETE \
                         -DSQLITE_MAX_VARIABLE_NUMBER=250000 \
                         -DSQLITE_MAX_EXPR_DEPTH=10000"

	./configure		\
	--prefix=/usr		\
	--libdir=/usr/lib32	\
	--disable-tcl

	make
}

package() {
	cd sqlite-src-$srcver

	make DESTDIR=$pkgdir install

	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
