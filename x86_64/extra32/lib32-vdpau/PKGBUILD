pkgname=lib32-vdpau
pkgver=1.4
pkgrel=2
pkgdesc='Nvidia VDPAU library'
arch=(x86_64)
url=https://gitlab.freedesktop.org/vdpau/libvdpau
depends=(lib32-libxext vdpau)
makedepends=(xorgproto git meson)
source=(git+https://gitlab.freedesktop.org/vdpau/libvdpau.git#tag=c3d1a9dbafdfe6144ff474d0d523dc01b068750f)

build() {
	export CC='gcc -m32'
	export CXX='g++ -m32'
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

	meson libvdpau build \
	--prefix=/usr \
	--libdir=/usr/lib32

	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
	rm -rf "${pkgdir}"/{etc,usr/include}
}
