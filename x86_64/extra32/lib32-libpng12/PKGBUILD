pkgname=lib32-libpng12
pkgver=1.2.59
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files'
arch=('x86_64')
depends=('glibc' 'lib32-zlib')
source=("https://sourceforge.net/projects/libpng/files/libpng-${pkgver}.tar.xz"
        "https://sourceforge.net/projects/libpng-apng/files/libpng12/${pkgver}/libpng-${pkgver}-apng.patch.gz")

prepare() {
	cd libpng-${pkgver}

	patch -Np1 -i ../libpng-${pkgver}-apng.patch

	libtoolize --force --copy
	aclocal
	autoconf
	automake --add-missing
}

build() {
	cd libpng-${pkgver}

	export CC='gcc -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	./configure \
	--prefix='/usr' \
	--libdir=/usr/lib32

	make
}

package() {
	cd libpng-${pkgver}

	make DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/usr/{bin,include,lib32/{libpng.{a,so,la},pkgconfig},share}
}
