pkgname=lib32-gnutls
pkgver=3.7.2
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
url="https://gnutls.org/"
depends=('lib32-nettle' 'lib32-p11-kit' 'lib32-libtasn1' 'lib32-libidn2' 'gnutls')
source=(https://www.gnupg.org/ftp/gcrypt/gnutls/v${pkgver%.*}/gnutls-${pkgver}.tar.xz)

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd gnutls-${pkgver}

	# disable libidn for security reasons:
	# http://lists.gnupg.org/pipermail/gnutls-devel/2015-May/007582.html

	./configure --prefix=/usr --libdir=/usr/lib32 --build=i686-pc-linux-gnu \
	--with-zlib \
	--with-included-unistring \
	--disable-static \
	--disable-guile \
	--disable-valgrind-tests

	make
}

package() {
	cd gnutls-${pkgver}
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}"/usr/{bin,include,share}
}