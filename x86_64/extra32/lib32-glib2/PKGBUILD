pkgname=lib32-glib2
pkgver=2.74.5
pkgrel=1
pkgdesc="Low level core library (32-bit)"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL)
arch=(x86_64)
depends=(lib32-pcre lib32-pcre2 lib32-libffi lib32-util-linux lib32-zlib glib2)
makedepends=(gettext gtk-doc shared-mime-info python3 git util-linux
             meson)
gvdbcommit=0854af0fdb6d527a8d1999835ac2c5059976c210
source=(git+https://gitlab.gnome.org/GNOME/glib.git#tag=$pkgver
	git+https://gitlab.gnome.org/GNOME/gvdb.git#commit=$gvdbcommit
    multilib.diff
    gio-querymodules-32.{hook,script})

prepare() {
    cd glib

    # Test suite workaround
    git apply -3 ../multilib.diff


	mkdir -p subprojects/
	cp -rf ../gvdb subprojects/
}

build() {
    export CC="gcc -m32 -mstackrealign"
    export CXX="g++ -m32 -mstackrealign"
    export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

    CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
    meson glib build \
    --prefix=/usr \
    -D glib_debug=disabled \
    -D selinux=disabled \
    --libdir=/usr/lib32

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    rm -r "$pkgdir"/usr/{share,include,libexec}
    find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
    mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

    install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
    install -D gio-querymodules-32.script "$pkgdir/usr/share/libalpm/scripts/gio-querymodules-32"
}
