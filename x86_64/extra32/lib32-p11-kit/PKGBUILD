pkgname=lib32-p11-kit
pkgver=0.24.0
pkgrel=2
pkgdesc="Loads and enumerates PKCS#11 modules (32-bit library)"
url="https://p11-glue.freedesktop.org"
arch=(x86_64)
depends=(lib32-libtasn1 lib32-libffi p11-kit)
makedepends=(meson)
source=(https://github.com/p11-glue/p11-kit/releases/download/$pkgver/p11-kit-$pkgver.tar.xz)

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

	meson p11-kit-$pkgver build \
	--prefix=/usr \
	--buildtype debugoptimized \
	--libdir /usr/lib32 \
	-D bash_completion=disabled \
	-D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source

	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	rm -rf "$pkgdir"/etc "$pkgdir"/usr/{bin,include,lib,libexec,share}
}