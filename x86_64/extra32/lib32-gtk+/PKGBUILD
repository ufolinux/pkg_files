pkgname=lib32-gtk+
pkgver=3.24.31
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit'
arch=(x86_64)
license=(LGPL)
url=https://www.gtk.org/
depends=(
	gtk+
	lib32-at-spi2-atk
	lib32-dbus
	lib32-mesa
	lib32-json-glib
	lib32-libepoxy
	lib32-libxcomposite
	lib32-libxcursor
	lib32-libxinerama
	lib32-libxkbcommon
	lib32-libxrandr
	lib32-pango
	lib32-wayland
	lib32-libxdamage
)
makedepends=(
	gtk-doc git meson ninja wayland-protocols
)
install=lib32-gtk+.install
_tag=ab45bde94c7bbd140b12fa0dd6203f7b98d1a715
source=(
  git+https://gitlab.gnome.org/GNOME/gtk.git#tag=${_tag}
  gtk-query-immodules-3.0-32.hook
)

build() {
  export CC='gcc -m32'
  export CXX=/bin/false
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  meson gtk build \
    --libdir=/usr/lib32 \
    -Dbroadway_backend=true \
    -Dcloudproviders=false \
    -Dcolord=yes \
    -Dgtk_doc=false \
    -Dintrospection=false \
    -Dman=false
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build

  rm -r "${pkgdir}"/{etc,usr/{include,share}}
  find "${pkgdir}"/usr/bin -type f -not -name gtk-query-immodules-3.0 -delete
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  install -Dm 644 gtk-query-immodules-3.0-32.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
}
