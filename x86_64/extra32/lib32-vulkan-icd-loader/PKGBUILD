pkgname=lib32-vulkan-icd-loader
pkgver=1.3.240
pkgrel=1
arch=(x86_64)
pkgdesc="Vulkan Installable Client Driver (ICD) Loader (32-bit)"
url="https://www.khronos.org/vulkan/"
makedepends=('cmake' 'python3' 'lib32-libx11' 'lib32-libxrandr' 'lib32-wayland' 'vulkan-headers')
source=("vulkan-icd-loader-${pkgver}.tar.gz::https://github.com/KhronosGroup/Vulkan-Loader/archive/v${pkgver}.tar.gz")

build() {
	export ASFLAGS=--32
	export CFLAGS=-m32
	export CXXFLAGS=-m32                                                                                                                                                                                                            
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig" 

	cd "${srcdir}"/Vulkan-Loader*

	rm -rf build ; mkdir build ; cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	-D VULKAN_HEADERS_INSTALL_DIR=/usr \
	-D CMAKE_INSTALL_SYSCONFDIR=/etc \
	-D CMAKE_INSTALL_LIBDIR=lib32 \
	-D CMAKE_INSTALL_DATADIR=/share \
	-D CMAKE_SKIP_RPATH=True \
	-D BUILD_TESTS=Off \
	-D BUILD_WSI_XCB_SUPPORT=On \
	-D BUILD_WSI_XLIB_SUPPORT=On \
	-D BUILD_WSI_WAYLAND_SUPPORT=On \
	-D CMAKE_BUILD_TYPE=Release \
	..

	make
}

package() {
	cd "${srcdir}"/Vulkan-Loader*/build
  
	make DESTDIR="${pkgdir}" install

	rm -rf $pkgdir/usr/share $pkgdir/usr/bin
}
