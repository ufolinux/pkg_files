pkgname=lib32-nss
rname=nss
pkgver=3.73.1
pkgrel=1
pkgdesc="Network Security Services"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
arch=(x86_64)
depends=(lib32-nspr lib32-sqlite3 lib32-zlib lib32-p11-kit)
makedepends=(perl python3)
source=("https://archive.mozilla.org/pub/security/$rname/releases/NSS_3_73_1_RTM/src/$rname-$pkgver.tar.gz")

build() {
	cd nss-$pkgver/nss

	./build.sh \
	--target ia32 \
	--opt \
	--system-sqlite \
	--system-nspr \
	--enable-libpkix \
	--disable-tests
}

package() {
	local nsprver="$(i686-pc-linux-gnu-pkg-config --modversion nspr)"
	local libdir=/usr/lib32

	sed nss-$pkgver/nss/pkg/pkg-config/nss.pc.in \
	-e "s,%libdir%,$libdir,g" \
	-e "s,%prefix%,/usr,g" \
	-e "s,%exec_prefix%,/usr/bin,g" \
	-e "s,%includedir%,/usr/include/nss,g" \
	-e "s,%NSPR_VERSION%,$nsprver,g" \
	-e "s,%NSS_VERSION%,$pkgver,g" |
	install -Dm644 /dev/stdin "$pkgdir$libdir/pkgconfig/nss.pc"

	ln -s nss.pc "$pkgdir$libdir/pkgconfig/mozilla-nss.pc"

	install -Dt "$pkgdir$libdir" nss-$pkgver/dist/Release/lib/*.so

	# Replace built-in trust with p11-kit connection
	ln -s pkcs11/p11-kit-trust.so "$pkgdir$libdir/p11-kit-trust.so"
	ln -sf p11-kit-trust.so "$pkgdir$libdir/libnssckbi.so"
}
