pkgname=lib32-openal
pkgver=1.21.1
pkgrel=1
arch=(x86_64)
# lib32-pipewire seems to have taken care of jack2 ( for some reason )
depends=(lib32-alsa-lib lib32-pulseaudio lib32-pipewire gcc openal lib32-ffmpeg)
makedepends=(git cmake ninja)
commitid=ae4eacf147e2c2340cc4e02a790df04c793ed0a9  # 1.21.1
source=("git+https://github.com/kcat/openal-soft#commit=$commitid")

prepare() {
	cd openal-soft

	# Missing include
	git cherry-pick -n 302e88dbf0f10224a5b87be4ce43b3fdd9d20184

	# https://bugs.archlinux.org/task/72729
	git cherry-pick -n b7ff1de48efda9ad54e6a3b4c1526722c371e832
}

build() {
	export CC="gcc -m32 -mstackrealign"
	export CXX="g++ -m32 -mstackrealign"
	export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

	cmake -S openal-soft -B build -G Ninja \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE=None \
	-DALSOFT_NO_CONFIG_UTIL=OFF \
	-DALSOFT_UTILS=OFF \
	-DALSOFT_INSTALL_UTILS=OFF \
	-DCMAKE_INSTALL_LIBDIR=lib32

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build

	rm -rv "$pkgdir"/usr/{include,share,bin}
}
