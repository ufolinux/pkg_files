_name=libjpeg-turbo
pkgname=lib32-libjpeg
pkgver=2.1.2
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
arch=(x86_64)
url="https://libjpeg-turbo.org/"
depends=(libjpeg)
makedepends=(nasm cmake)
source=("https://sourceforge.net/projects/${_name}/files/${pkgver}/${_name}-${pkgver}.tar.gz")

build() {
	cd "${_name}-$pkgver"

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
	-DCMAKE_BUILD_TYPE=None \
	-DWITH_JPEG8=ON \
	-W no-dev \
	-B build \
	-S .

	make VERBOSE=1 -C build
}

package() {
	cd "${_name}-$pkgver"

	make \
	VERBOSE=1 DESTDIR="${pkgdir}" \
	docdir="/usr/share/doc/${_name}" \
	exampledir="/usr/share/doc/${_name}" \
	install -C build

	# remove everything that is provided by libjpeg-turbo
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
