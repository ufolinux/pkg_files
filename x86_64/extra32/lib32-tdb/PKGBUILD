_pkgbasename=tdb
pkgname=lib32-$_pkgbasename
pkgver=1.4.5
pkgrel=1
pkgdesc="A Trivial Database similar to GDBM but allows simultaneous commits (32-bit)"
arch=(x86_64)
depends=($_pkgbasename)
makedepends=(libxslt python3)
source=(https://www.samba.org/ftp/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.{gz,asc})

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd ${_pkgbasename}-${pkgver}

	./configure --prefix=/usr \
	--localstatedir=/var \
	--sysconfdir=/etc/samba \
	--libdir=/usr/lib32 \
	--disable-python

	make
}

package() {
	cd ${_pkgbasename}-${pkgver}

	make -j1  DESTDIR="$pkgdir" install
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}