pkgname=lib32-libglvnd
pkgver=1.4.0
pkgrel=3
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
depends=('libglvnd')
makedepends=('lib32-libx11' 'lib32-libxext' 'xorgproto' 'python3' 'meson')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/glvnd/libglvnd/-/archive/v${pkgver}/libglvnd-v${pkgver}.tar.gz")


build() {
	export CC='gcc -m32'
	export CXX='g++ -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	meson libglvnd-v$pkgver build \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	-D headers=false

	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install

	rm -r "$pkgdir"/usr/include
}
