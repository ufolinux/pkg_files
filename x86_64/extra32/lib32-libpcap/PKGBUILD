pkgname=lib32-libpcap
pkgver=1.10.1
pkgrel=1
arch=('x86_64')
depends=('glibc' 'lib32-libnl' "libpcap=${pkgver}")
makedepends=('bluez' 'lib32-dbus')
source=("https://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz")

prepare() {
	mv -v "libpcap-${pkgver}" "${pkgname}-${pkgver}"
	cd "${pkgname}-${pkgver}"
	autoreconf -fiv
}

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd "${pkgname}-${pkgver}"
	./configure \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	--enable-ipv6 \
	--enable-bluetooth \
	--enable-usb \
	--with-libnl

	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR=${pkgdir} install

	# remove files provided by libpcap
	rm -rf "${pkgdir}"/usr/lib32/libpcap.a
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}