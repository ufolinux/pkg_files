pkgname=lib32-libxslt
pkgver=1.1.35
pkgrel=1
arch=('x86_64')
depends=('lib32-libxml2' 'lib32-libgcrypt' 'libxslt' 'lib32-xz')
makedepends=(git)
commitid=50af4e65da712ceb59f98a7feeeab2ddc3324ab9  # v1.1.35
source=("git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=$commitid")

prepare() {
	cd libxslt

	NOCONFIGURE=1 ./autogen.sh
}

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd libxslt
	./configure --prefix=/usr --libdir=/usr/lib32 --without-python

	make
}

package() {
	cd libxslt
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}"/usr/{include,share,bin}
	rm -f "${pkgdir}"/usr/lib/python*/site-packages/*.a
}
