_pkgbasename=libx11
pkgname=lib32-$_pkgbasename
pkgver=1.7.3.1
pkgrel=2
pkgdesc="X11 client-side library (32-bit)"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
depends=('lib32-libxcb' $_pkgbasename)
makedepends=('util-macros' 'xorgproto' 'libxtrans')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.xz)
build() {
        export CC="gcc -m32"
        export CXX="g++ -m32"

	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd libX11-${pkgver}

	./configure		\
	--prefix=/usr		\
	--disable-xf86bigfont	\
	--libdir=/usr/lib32	\
	--disable-specs

	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd libX11-${pkgver}
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}"/usr/{include,share}
}
