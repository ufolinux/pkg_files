pkgname=lib32-jansson
pkgver=2.14
pkgrel=1
pkgdesc='C library for encoding, decoding and manipulating JSON data'
arch=(x86_64)
depends=(jansson)
makedepends=(git)
_tag=684e18c927e89615c2d501737e90018f4930d6c5
source=(git+https://github.com/akheron/jansson.git#tag=${_tag})

prepare() {
	cd jansson

	autoreconf -fiv
}

build() {
	cd jansson

	export CC='gcc -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	./configure \
	--prefix=/usr \
	--libdir=/usr/lib32

	make
}

package() {
	make DESTDIR="${pkgdir}" -C jansson install
	rm -rf "${pkgdir}"/usr/{share,include}
}