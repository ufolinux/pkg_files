pkgname=lib32-libcanberra
pkgver=0.30
pkgrel=1
arch=(x86_64)
depends=(lib32-libvorbis lib32-alsa-lib lib32-pulseaudio libcanberra lib32-libltdl)
makedepends=(git)
_commit=c0620e432650e81062c1967cc669829dbd29b310  # master
source=("git+http://git.0pointer.net/clone/libcanberra.git#commit=$_commit"
        libcanberra-multi-backend.patch)

prepare() {
  cd libcanberra

	# https://bugs.archlinux.org/task/71341
	# https://bugs.freedesktop.org/show_bug.cgi?id=51662
	git apply -3 ../libcanberra-multi-backend.patch

	./autogen.sh
}

build() {
	cd libcanberra

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

	./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
	--libdir=/usr/lib32 \
	--with-builtin=dso --enable-null --disable-oss \
	--enable-alsa --enable-pulse \
	--with-systemdsystemunitdir=/usr/lib/systemd/system --disable-gtk-doc

	make
}

package() {
	cd libcanberra

	make -j1 DESTDIR="$pkgdir" install

	rm -f "$pkgdir"/usr/lib32/*.la
	rm -f "$pkgdir"/usr/lib32/gtk-{2,3}.0/modules/*.la

	rm -fr "$pkgdir"/usr/{include,share,bin,lib,lib32/gnome-settings-daemon-3.0}
}
