pkgname=lib32-gtk3+
pkgver=3.24.31
pkgrel=1
arch=(x86_64)
depends=(
    gtk3+ lib32-at-spi2-atk lib32-colord lib32-dbus lib32-mesa lib32-gdk-pixbuf lib32-json-glib
    lib32-cups lib32-libepoxy lib32-libxcomposite lib32-libxcursor lib32-libxinerama
    lib32-libxkbcommon lib32-libxrandr lib32-pango lib32-rest lib32-libcanberra
    lib32-wayland lib32-libxdamage
)
makedepends=(
    gtk-doc git glib2 meson ninja sassc wayland-protocols
)
install=lib32-gtk3+.install
ltag=ab45bde94c7bbd140b12fa0dd6203f7b98d1a715

source=(
  git+https://gitlab.gnome.org/GNOME/gtk.git#tag=${ltag}
  gtk-query-immodules-3.0-32.hook
)

build() {
    export CC='gcc -m32'
    export CXX=/bin/false
    export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

    meson gtk build \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    -Dbroadway_backend=true \
    -Dcloudproviders=false \
    -Dcolord=yes \
    -Dgtk_doc=false \
    -Dintrospection=false \
    -Dman=false

    meson compile -C build
}

package() {
    DESTDIR="${pkgdir}" meson install -C build

    rm -r "${pkgdir}"/{etc,usr/{include,share}}
    find "${pkgdir}"/usr/bin -type f -not -name gtk-query-immodules-3.0 -delete
    mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
    install -Dm 644 gtk-query-immodules-3.0-32.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
}
