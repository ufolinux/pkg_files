pkgname=lib32-icu
pkgver=70.1
pkgrel=1
pkgdesc="International Components for Unicode library (32 bit)"
arch=('x86_64')
depends=('icu')
provides=(libicu{data,i18n,io,test,tu,uc}.so)
source=(https://github.com/unicode-org/icu/releases/download/release-${pkgver//./-}/icu4c-${pkgver//./_}-src.tgz)

build() {
	export CC='gcc -m32'
	export CXX='g++ -m32'
	export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

	cd icu/source

	./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32

	make
}

package() {
	cd icu/source

	make -j1 DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}/usr"/{include,sbin,share}

	# keep icu-config-32
	find "${pkgdir}/usr/bin" -type f -not -name icu-config -delete
	mv "${pkgdir}/usr/bin"/icu-config{,-32}

	install -d m644 "${pkgdir}/usr/share/licenses"
	ln -s icu "${pkgdir}/usr/share/licenses/${pkgname}"
}