pkgname=lib32-e2fsprogs
pkgver=1.46.5
pkgrel=1
pkgdesc='Ext2/3/4 filesystem libraries (32-bit)'
arch=('x86_64')
url='http://e2fsprogs.sourceforge.net'
depends=('e2fsprogs')
makedepends=('bc' 'lib32-util-linux')
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/e2fsprogs/v${pkgver}/e2fsprogs-${pkgver}.tar.xz")

build() {
	export CC='gcc -m32'
	export CXX='g++ -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	cd "${srcdir}/e2fsprogs-${pkgver}"

	./configure --prefix=/usr \
	--libdir=/usr/lib32 \
	--with-root-prefix='' \
	--enable-elf-shlibs \
	--disable-{debugfs,imager,resizer,fsck,uuidd,libuuid,libblkid}

	make
}

package() {
	cd "${srcdir}/e2fsprogs-${pkgver}"

	make DESTDIR="${pkgdir}" install-libs

	rm -rf "${pkgdir}"/usr/{bin,include,share}
}