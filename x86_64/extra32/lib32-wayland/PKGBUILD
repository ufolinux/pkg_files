pkgname=lib32-wayland
pkgver=1.20.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=(x86_64)
url=https://wayland.freedesktop.org
license=(MIT)
depends=(
	lib32-libffi
	lib32-expat
	lib32-libxml2
	wayland
)
makedepends=(meson)
source=("https://wayland.freedesktop.org/releases/wayland-$pkgver.tar.xz")

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	meson build wayland-$pkgver \
	--buildtype=release \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	-D documentation=false

	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
	rm -rf "${pkgdir}"/usr/{bin,include,share}
}
