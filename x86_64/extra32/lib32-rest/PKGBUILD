pkgname=lib32-rest
pkgver=0.8.1
pkgrel=1
arch=(x86_64)
depends=(rest lib32-libsoup)
makedepends=(git)
_tag=2971b3b92701f70fc368ad64bd25cefaea043f87
source=(git+https://gitlab.gnome.org/GNOME/librest.git#tag=${_tag})

prepare() {
  cd librest
  NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd librest

	export CC='gcc -m32'
	export CXX='g++ -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	./configure \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	--disable-introspection \
	--without-gnome \
	--disable-gtk-doc
	make
}

package() {
	make DESTDIR="${pkgdir}" -C librest install
	rm -rf "${pkgdir}"/usr/{include,share}
}
